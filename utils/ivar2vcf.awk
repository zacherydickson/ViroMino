#!/usr/bin/awk -f

#Assumes the current line is a header, 
#Sets the global ColIdx value to match the header
function ParseHeader(){
    split("",ColIdx,"");
    for(i=1;i<=NF;i++){
        ColIdx[$i]=i;
    }
}

BEGIN {
    if(ARGC -1 < 1){
        print "Provide a tsv file generated by ivar" > "/dev/stderr"
        ExitState=1;
        exit ExitState;
    }
    if(!Ref){
        print "Provide a value for the Variable Ref in the format chr:Len"
        ExitState=1;
        exit ExitState;
    }
    split(Ref,a,":");
    refID=a[1]; refLen=a[2];
    #Print the VCF header
    print "##fileformat=VCFv4.0"
    print "##contig=<ID="refID",length="refLen">"
    print "##source=ivar variants"
    print "##INFO=<ID=DP,Number=1,Type=Integer,Description=\"Raw Depth\">"
    print "##INFO=<ID=AF,Number=1,Type=Float,Description=\"Allele Frequency\">"
    print "##INFO=<ID=DP4,Number=4,Type=Integer,Description=\"Counts for ref-forward, ref-reverse, alt-forward and alt-reverse bases\">"
    print "##INFO=<ID=INDEL,Number=0,Type=Flag,Description=\"Indicates that the variant is an INDEL\">"
    print "##FORMAT=<ID=GT,Number=1,Type=String,Description=\"Genotype\">"
    print "##FORMAT=<ID=AD,Number=R,Type=Integer,Description=\"Number of observations for each allele\">"
    print "#CHROM\tPOS\tID\tREF\tALT\tQUAL\tFILTER\tINFO\tFORMAT\tunknown"
    OFS="\t"
}

(FNR == 1){
    ParseHeader()
    next;
}
#Skip if Ivar thinks it fails
($ColIdx["PASS"] == "FALSE"){next}
#Skip if there isn't at least one read supporting both the ref and alt alleles
($ColIdx["REF_DP"] == 0 || $ColIdx["ALT_DP"] == 0){next}

{
    chr=$ColIdx["REGION"]
    pos=$ColIdx["POS"]
    id="."
    ref=$ColIdx["REF"]
    alt=$ColIdx["ALT"]
    filt="PASS"
    dp=$ColIdx["TOTAL_DP"]
    af=$ColIdx["ALT_FREQ"]
    dp4_rf=$ColIdx["REF_DP"]-$ColIdx["REF_RV"]
    dp4_rr=$ColIdx["REF_RV"]
    dp4_af=$ColIdx["ALT_DP"]-$ColIdx["ALT_RV"]
    dp4_ar=$ColIdx["ALT_RV"]
    dp4=dp4_rf","dp4_rr","dp4_af","dp4_ar
    formatSpec="GT:AD"
    gt=(af > 0.5) ? 1 : 0;
    ad=$ColIdx["REF_DP"]","$ColIdx["ALT_DP"]
    info="DP="dp";AF="af";DP4="dp4
    if(substr(alt,1,1) == "+"){
        alt = ref substr(alt,2)
        info=info";INDEL"
    } else if (substr(alt,1,1) == "-"){
        ref = ref substr(alt,2)
        alt = substr(ref,1,1)
        info=info";INDEL"
    }
    qual=$ColIdx["ALT_QUAL"]
    format=gt":"ad
    print chr,pos,id,ref,alt,qual,filt,info,formatSpec,format
}


END {
    if(ExitState){exit ExitState}
}
